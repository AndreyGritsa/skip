module SKCSV;

base class Token {
  children =
  | NewLine()
  | Comma()
  | Chars(Array<Char>)
}

fun lexRaw(next: () -> Char): mutable Iterator<Token> {
  acc = mutable Vector[];
  lastIsString = false;
  loop {
    next() match {
    | '\n' ->
      if (!lastIsString) {
        str = acc.toArray();
        acc.clear();
        yield Chars(str);
      };
      break void
    | ',' ->
      if (!lastIsString) {
        str = acc.toArray();
        acc.clear();
        yield Chars(str);
      };
      !lastIsString = false;

      yield Comma()
    | '"' ->
      !lastIsString = true;
      acc.push('"');
      loop {
        c = next();
        acc.push(c);
        if (c == '"') {
          break void;
        }
      };
      str = acc.toArray();
      acc.clear();
      yield Chars(str)
    | x ->
      !lastIsString = false;
      acc.push(x)
    }
  }
}

base class CValue {
  children =
  | CInt(Int)
  | CFloat(Float)
  | CString(String)
}

fun trim(chars: Array<Char>): Array<Char> {
  i = 0;
  while (i < chars.size() && chars[i] == ' ') !i = i + 1;
  j = chars.size();
  while (j - 1 >= 0 && chars[j - 1] == ' ') !j = j - 1;
  chars.slice(i, j)
}

fun lex(next: () -> Char): mutable Iterator<String> {
  stringAcc: ?mutable Vector<Char> = None();
  for (tok in lexRaw(next)) {
    (tok, stringAcc) match {
    | (Chars(chars), Some(acc)) if (chars.size() > 0 && chars[0] == '"') ->
      !chars = trim(chars);
      acc.extend(chars.slice(0, chars.size() - 1))
    | (
      Chars(chars),
      None(),
    ) if (chars.size() > 0 && chars[chars.size() - 1] == '"') ->
      !chars = trim(chars);
      !chars = chars.slice(1, chars.size() - 1);
      !stringAcc = Some(Vector::mcreateFromItems(chars))
    | _ ->
      stringAcc match {
      | None() -> void
      | Some(acc) ->
        !stringAcc = None();
        yield String::fromChars(acc.toArray())
      };
      tok match {
      | Chars(chars) -> yield String::fromChars(trim(chars))
      | NewLine() -> break void
      | Comma() -> void
      }
    }
  };
  stringAcc match {
  | None() -> void
  | Some(acc) ->
    !stringAcc = None();
    yield String::fromChars(acc.toArray())
  }
}

fun insert(
  context: mutable SKFS.Context,
  options: SKSQL.Options,
  tableName: String,
): ?((mutable SKFS.Context, mutable SKFS.Context) ~> void) {
  baseName = SKFS.SID::create(tableName);

  eval = SKSQL.Evaluator{options};
  pos = 0;
  inTransaction = false;
  paramsOpt = None();
  table = SKSQL.getTable(options, context, pos, baseName);
  values = mutable Vector[];
  for (_ in Range(0, 1000)) {
    for (value in lex(() -> getChar(0))) {
      values.push(value);
    };
    cvalues = values.mapWithIndex((idx, val) -> {
      cval = SKSQL.parseCSVValue(val);
      SKSQL.valueToCValue(pos, table.schema, idx, cval)
    });
    f = eval.insert(
      context,
      pos,
      inTransaction,
      table,
      paramsOpt,
      cvalues.toArray(),
    );
    if (f is Some _) return f;
    values.clear();
  };
  None()
}
