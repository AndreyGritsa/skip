module SKFS;

fun reduceCapacity<T>(capacity: Int, prevList: List<T>): List<T> {
  if (capacity == 0) return List.Nil();
  prevList match {
  | nil @ List.Nil() -> nil
  | List.Cons(x, rl) -> List.Cons(x, reduceCapacity(capacity - 1, rl))
  }
}

type MapFun = (mutable Context, mutable Writer, BaseName, Array<File>) ~> void;

class FileNotFound(path: Path) extends SKFS.Exception {
  fun toString(): String {
    `File not found: ${this.path}`
  }
}
class DuplicateFiles(path: Path) extends SKFS.Exception {
  fun toString(): String {
    `Duplicate values found for path: ${this.path}`
  }
}

value class FixedRow<T>(
  key: BaseName,
  value: (Path, T),
) uses Hashable, Orderable {
  fun toKeyPath(): (BaseName, Path) {
    (this.key, this.value.i0);
  }

  fun compare(row: FixedRow<T>): Order {
    this.toKeyPath().compare(row.toKeyPath())
  }

  fun hash(): Int {
    this.toKeyPath().hash()
  }
}

value class RowMapped<T>(
  key: BaseName,
  reads: Vector<Path>,
  values: Array<FixedRow<T>>,
)

value class GroupedRows<T>(
  rows: Array<FixedRow<T>>,
  old: Array<FixedRow<BaseName>>,
)

class FixedData<T>{dirName: DirName, data: Array<FixedRow<T>> = Array[]} {
  fun getPos(key: BaseName, i: Int, j: Int): Int {
    if (i > j) {
      return i;
    };
    pivot = (i + j) / 2;
    key.compare(this.data[pivot].key) match {
    | LT()
    | EQ() ->
      this.getPos(key, i, pivot - 1)
    | GT() -> this.getPos(key, pivot + 1, j)
    }
  }

  fun getArray(key: BaseName): Array<(Path, T)> {
    pos = this.getPos(key, 0, this.data.size() - 1);
    result = mutable Vector[];
    while (pos < this.data.size() && this.data[pos].key == key) {
      result.push(this.data[pos].value);
      !pos = pos + 1;
    };
    result.toArray();
  }

  fun maybeGet(key: BaseName): ?(Path, T) {
    arr = this.getArray(key);
    if (arr.size() > 2) {
      path = Path(this.dirName, key);
      throw (DuplicateFiles(path))
    };
    if (arr.size() == 0) {
      return None();
    };
    Some(arr[0])
  }

  fun get(key: BaseName): (Path, T) {
    this.maybeGet(key) match {
    | None() ->
      path = Path(this.dirName, key);
      throw (FileNotFound(path))
    | Some(x) -> x
    }
  }
}

class Partition{
  time: Int,
  baseDirName: String,
  dirs: Dir,
  getKey: File ~> BaseName,
  keys: SortedMap<BaseName, SortedSet<BaseName>>,
} {
  static fun dirName(baseDirName: String, key: BaseName): DirName {
    DirName::create(baseDirName + key.toString() + "/");
  }
}

base class PlaceHolder {
  children =
  | PHEmpty()
  | PHProcessing()
  | PHDone(File)
}

class Dir{
  time: Int,
  dirName: DirName,
  isInput: Bool,
  fixedData: FixedData<Array<File>>,
  fixedOld: SortedMap<DirName, FixedData<BaseName>> = SortedMap[],
  data: SortedMap<BaseName, SortedMap<Path, Array<File>>> = SortedMap[],
  old: SortedMap<DirName, SortedMap<BaseName, Array<BaseName>>> = SortedMap[],
  childDirs: SortedMap<Int, DirName> = SortedMap[],
  dirty: SortedSet<BaseName> = SortedSet[],
  dirtyReaders: SortedSet<Path> = SortedSet[],
  partitions: SortedMap<Int, Partition> = SortedMap[],
  mapFun: ?MapFun = None(),
  lazyFun: ?((mutable Context, DirName, BaseName) ~> File) = None(),
} extends File {
  private fun compactData(): FixedData<Array<File>> {
    fixedData = this.fixedData.data;
    current = 0;
    newData = mutable Vector[];
    for (newKey => values in this.data) {
      while (current < fixedData.size() && fixedData[current].key < newKey) {
        newData.push(fixedData[current]);
      };
      for (path => array in values) {
        newData.push(FixedRow(fixedData[current].key, (path, array)));
      };
      !current = current + 1;
    };
    while (current < fixedData.size()) {
      newData.push(fixedData[current]);
      !current = current + 1;
    };
    this.fixedData with {data => newData.toArray()}
  }

  fun files(): Set<BaseName> {
    fixedData = this.fixedData.data;
    current = 0;
    keys = mutable Set[];
    for (newKey => newValues in this.data) {
      while (current < fixedData.size() && fixedData[current].key < newKey) {
        row = fixedData[current];
        if (row.value.i1.size() > 0) {
          keys.insert(row.key);
        };
        !current = current + 1;
      };
      if (newValues.size() > 0) {
        keys.insert(newKey);
      };
      !current = current + 1;
    };
    while (current < fixedData.size()) {
      row = fixedData[current];
      if (row.value.i1.size() > 0) {
        keys.insert(row.key);
      };
      !current = current + 1;
    };
    freeze(keys)
  }

  /*

    private fun compactOld(): SortedMap<DirName, FixedData<BaseName>> {
      fixedOld = this.fixedOld;
      !fixedOld = fixedOld.map((dirName, fixed) -> {
        if (!this.old.containsKey(dirName)) {
          return fixed;
        };
        fixedData = fixed.data;
        old = this.old[dirName];
        current = 0;
        newOld = mutable Vector[];
        for (newKey => values in old) {
          while (current < fixedData.size() && fixedData[current].key < newKey) {
            newOld.push(fixedData[current]);
          };
          for (value in values) {
            newOld.push(FixedRow(fixedData[current].key, value));
          };
          !current = current + 1;
        };
        while (current < fixedData.size()) {
          newOld.push(fixedData[current]);
          !current = current + 1;
        };
        fixed with {data => newOld.toArray()}
      });
      for (dirName => map in this.old) {
        if (fixedOld.containsKey(dirName)) {
          continue;
        };
        newOld = mutable Vector[];
        for (baseName => values in map) {
          for (value in values) {
            newOld.push(FixedRow(baseName, value));
          }
        };
        !fixedOld = fixedOld.add(
          dirName,
          FixedData{dirName, data => newOld.toArray()},
        );
      };
      fixedOld
    }

    fun compact(): this {
      invariant(this.dirty.isEmpty());
      invariant(this.dirtyReaders.isEmpty());
      fixedOld = this.compactOld();
      fixedData = this.compactData();
      this with {fixedOld, fixedData, old => SortedMap[], data => SortedMap[]}
    }
  */

  static fun lazy(
    context: mutable Context,
    dirName: DirName,
    f: (mutable Context, DirName, BaseName) ~> File,
  ): Dir {
    time = context.timeStamp();
    updateReaders = (
      dirName: DirName,
      context: mutable Context,
      key: BaseName,
    ) ~> {
      reads = context.getReads();

      for (read in reads) {
        readSet = context.newDeps.maybeGet(read) match {
        | None() -> SortedSet[]
        | Some(x) -> x
        };
        !readSet = readSet.set((dirName, dirName, key));
        context.!newDeps[read] = readSet;
      };
    };
    lazyFun = (context: mutable Context, dirName: DirName, key: BaseName) ~> {
      reads = context.reads;
      context.!reads = mutable Vector[];

      result = try {
        f(context, dirName, key)
      } catch {
      | e ->
        ErrorFile(
          List[ErrorTrace(dirName.toString(), key.toString(), e.getMessage())],
        )
      };
      dir = context.getDir(dirName);
      results = Array[result];
      map = SortedMap[Path(dirName, key) => results];
      !dir.data = dir.data.set(key, map);
      updateReaders(dirName, context, key);
      context.setDir(dirName, dir);
      if (context.debugMode) {
        name = "" + dir.dirName + key;
        print_string(`WRITTEN1:  ${name}`);
      };
      context.!reads = reads;
      result
    };
    newDir = Dir{
      time,
      dirName,
      isInput => false,
      fixedData => FixedData{dirName, data => Array[]},
      lazyFun => Some(lazyFun),
    };
    context.setDir(dirName, newDir);
    newDir
  }

  fun getDirName(): DirName {
    this.dirName
  }

  fun getOld(dirName: DirName, key: BaseName): Array<BaseName> {
    if (this.old.containsKey(dirName) && this.old[dirName].containsKey(key)) {
      this.old[dirName][key]
    } else {
      this.fixedOld[dirName].getArray(key).map(x -> x.i1)
    }
  }

  fun getArrayRaw(key: BaseName): Array<File> {
    fixed = this.fixedData.getArray(key);
    this.data.maybeGet(key) match {
    | None() -> fixed.map(x -> x.i1).flatten()
    | Some(modified) ->
      result = mutable Vector<(Path, Array<File>)>[];
      current = 0;
      for (path => values in modified) {
        while (current < fixed.size() && fixed[current].i0 < path) {
          result.push(fixed[current]);
          !current = current + 1;
        };
        while (current < fixed.size() && fixed[current].i0 == path) {
          !current = current + 1;
        };
        result.push((path, values));
      };
      while (current < fixed.size()) {
        result.push(fixed[current]);
        !current = current + 1;
      };
      result.map(x -> x.i1).flatten().toArray()
    }
  }

  fun getArray(context: mutable Context, key: BaseName): Array<File> {
    path = Path(this.dirName, key);
    context.addDep(path);
    values = this.getArrayRaw(key);
    if (values.size() != 0) return values;
    this.lazyFun match {
    | None() -> values
    | Some(f) -> Array[f(context, this.dirName, key)]
    }
  }

  fun mapRow(
    context: mutable Context,
    key: BaseName,
    values: Array<File>,
    f: MapFun,
  ): RowMapped<Array<File>> {
    writer = mutable Writer{};
    f(context, writer, key, values);
    kvArray = freeze(writer).getWrites();
    reads = context.getReads();
    rows = kvArray.map(kv2 -> {
      (k2, v2) = kv2;
      path = Path(this.dirName, key);
      FixedRow(k2, (path, v2))
    });
    RowMapped(key, reads, rows)
  }

  fun regroupMap<File2>(mapped: Vector<RowMapped<File2>>): GroupedRows<File2> {
    rowVec = mutable Vector[];
    oldVec = mutable Vector[];
    for (rowMapped in mapped) {
      key = rowMapped.key;
      rows = rowMapped.values;
      for (row in rows) {
        oldVec.push(FixedRow(key, (Path(this.dirName, key), row.key)));
        rowVec.push(row);
      };
    };
    rowVec.sort();
    oldVec.sort();
    GroupedRows(rowVec.toArray(), oldVec.toArray())
  }

  fun updateReaders(
    context: mutable Context,
    dirName: DirName,
    mapped: readonly Vector<RowMapped<Array<File>>>,
  ): void {
    readDirs = mutable Map[];
    deps = mutable Vector[];
    for (row in mapped) {
      for (read in row.reads) {
        if (!readDirs.containsKey(read.dirName)) {
          readDirs![read.dirName] = mutable Vector[];
        };
        readDirs[read.dirName].push((read.baseName, row.key));
      }
    };
    for (readDirName => reads in readDirs) {
      reads.sort();
      uniqReads = reads.unique();
      for (row in uniqReads) {
        readerKey = row.i0;
        readKey = row.i1;
        readerPath = Path(readDirName, readerKey);
        deps.push((readerPath, this.dirName, dirName, readKey));
      };
    };
    context.!deps = List.Cons(freeze(deps), context.deps);
  }

  fun mapData<File2: File>(
    context: mutable Context,
    f: MapFun,
  ): Vector<RowMapped<Array<File>>> {
    current = 0;
    mapped = mutable Vector[];
    addFixed = (fixedRow: FixedRow<Array<File>>) -> {
      key = fixedRow.key;
      valueAcc = mutable Vector<(Path, Array<File>)>[];
      while (
        current < this.fixedData.data.size() &&
        this.fixedData.data[current].key == key
      ) {
        valueAcc.push(fixedRow.value);
        !current = current + 1;
      };
      fixedFiles = valueAcc.map(x -> x.i1).flatten().toArray();
      mapped.push(this.mapRow(context, key, fixedFiles, f));
    };
    for (key => _ in this.data) {
      while (
        current < this.fixedData.data.size() &&
        this.fixedData.data[current].key < key
      ) {
        addFixed(this.fixedData.data[current]);
      };
      while (
        current < this.fixedData.data.size() &&
        this.fixedData.data[current].key == key
      ) {
        !current = current + 1;
      };
      mapped.push(this.mapRow(context, key, this.getArrayRaw(key), f));
    };
    while (current < this.fixedData.data.size()) {
      addFixed(this.fixedData.data[current]);
    };
    freeze(mapped)
  }

  fun apply(
    context: mutable Context,
    dirNameStr: String,
    f: MapFun,
    extraDirs: Array<Dir> = Array[],
  ): Dir {
    dirName = DirName::create(dirNameStr);
    dirs = mutable Vector[this];
    mappedAcc = mutable Vector[this.mapData(context, f)];
    extraDirs.each(dir -> {
      dirs.push(dir);
      mappedAcc.push(dir.mapData(context, f))
    });
    mapped = mappedAcc.flatten();
    grouped = this.regroupMap(mapped);
    this.updateReaders(context, dirName, mapped);
    time = context.timeStamp();
    fixedData = FixedData{dirName, data => grouped.rows};
    dir = Dir{time, dirName, isInput => false, fixedData, mapFun => Some(f)};
    context.setDir(dirName, dir);
    for (parent in dirs) {
      !parent.fixedOld[dirName] = FixedData{dirName, data => grouped.old};
      !parent.childDirs = parent.childDirs.add(time, dirName);
      context.setDir(parent.dirName, parent);
    };
    dir
  }

  fun updatePartition(context: mutable Context, partition: Partition): void {
    for (key in this.dirty) {
      dirName = Partition::dirName(partition.baseDirName, key);
      source = Path(this.dirName, key);
      values = this.getArrayRaw(key);
      oldKeys = partition.keys.maybeGet(key) match {
      | None() -> SortedSet[]
      | Some(x) -> x
      };
      newKeys = SortedSet[];
      mdata = mutable Map[];
      for (value in values) {
        subKey = partition.getKey(value);
        !oldKeys = oldKeys.remove(subKey);
        !newKeys = newKeys.set(subKey);
      };
      for (oldKey in oldKeys) {
        mdata![oldKey] = mutable Vector[];
      };
      for (value in values) {
        subKey = partition.getKey(value);
        if (!mdata.containsKey(subKey)) {
          mdata![subKey] = mutable Vector[];
        };
        mdata[subKey].push(value);
      };
      for (subKey => valueVec in mdata) {
        dir = context.maybeGetDir(dirName) match {
        | None() ->
          time = context.timeStamp();
          fixedData = FixedData{dirName, data => Array[]};
          Dir{time, dirName, isInput => false, fixedData}
        | Some(x) -> x
        };
        !dir = dir.writeEntry(context, source, subKey, valueVec.toArray());
        context.setDir(dirName, dir);
        !partition.keys[key] = newKeys;
        !partition.dirs = partition.dirs.writeEntry(
          context,
          source,
          key,
          Array[dir],
        );
        !this.partitions[partition.time] = partition;
        context.setDir(this.dirName, this);
      }
    };
  }

  fun update(context: mutable Context): void {
    // We have been removed from the set of directories.
    // This can happen when an old dependency changed.
    // Because of the change, the directory gets added
    // to the list of directories to update. But in the mean time
    // the source no longer creates this directory.
    if (!context.state.dirs.state.containsKey(this.dirName)) {
      return void;
    };

    if (this.dirty.isEmpty() && this.dirtyReaders.isEmpty()) return void;

    dirtyDeps = mutable Map[];

    for (reader in this.dirtyReaders) {
      dirName = reader.dirName;
      baseName = reader.baseName;
      if (!dirtyDeps.containsKey(dirName)) {
        dirtyDeps![dirName] = mutable Set[];
      };
      dirtyDeps[dirName].insert(baseName);
    };

    this.lazyFun match {
    | None() -> void
    | Some(f) ->
      dirtyReaders = fixPoint(this.dirtyReaders, key -> {
        context
          .getDeps(key)
          .filter(path -> path.i1 == this.dirName)
          .map(x -> Path(x.i1, x.i2))
          .collect(Set);
      });
      for (path in dirtyReaders) {
        key = path.baseName;
        map = SortedMap[path => Array[]];
        !this.data = this.data.set(key, map);
      };
      context.setDir(this.dirName, this);
      for (path in dirtyReaders) {
        key = path.baseName;
        _ = f(context, this.dirName, key);
      };
      !this = context.getDir(this.dirName);
      for (path in dirtyReaders) {
        this.updateDirtyReaders(context, path);
      };
      !this.dirty = SortedSet::createFromItems(
        dirtyReaders.map(x -> x.baseName),
      )
    };

    for (partition in this.partitions) {
      this.updatePartition(context, partition)
    };

    for (childName in this.childDirs) {
      debug(("CHILD", childName));
      child = context.state.dirs.get(childName);
      dirty = if (dirtyDeps.containsKey(childName)) {
        dirtyDeps[childName]
      } else {
        mutable Set[]
      };
      for (key in this.dirty) {
        dirty.insert(key);
      };
      for (key in dirty) {
        values = this.getArrayRaw(key);
        source = Path(this.dirName, key);
        oldKeys = Set::mcreateFromItems(this.getOld(childName, key));
        mapFun = child.mapFun match {
        | None() -> invariant_violation("Could no find mapFun")
        | Some(f) -> f
        };
        (!child, newKeys) = child.updateChildForKey(
          mapFun,
          context,
          oldKeys,
          source,
          key,
          values,
        );
        // We need to remember what keys we produced for the next
        // time around.
        oldKeysMap = this.old.maybeGet(child.dirName) match {
        | None() -> SortedMap[]
        | Some(x) -> x
        };
        !oldKeysMap[key] = newKeys;
        !this.old[child.dirName] = oldKeysMap;
      };
      context.setDir(child.dirName, child);
    };

    !this.dirty = SortedSet[];
    !this.dirtyReaders = SortedSet[];
    context.setDir(this.dirName, this);
  }

  fun union(context: mutable Context, name: String, dirs: Array<Dir>): Dir {
    this.apply(context, name, (_, w, x, y) ~> w.writeArray(x, y), dirs)
  }

  fun partition(
    context: mutable Context,
    baseDirName: String,
    getKey: (File) ~> BaseName,
  ): Dir {
    current = 0;
    dirs = mutable Vector[];
    keys = SortedMap[];
    getFixedFiles = (fixedRow: FixedRow<Array<File>>) -> {
      key = fixedRow.key;
      subKeys = SortedSet[];
      rows = mutable Vector<FixedRow<Array<File>>>[];
      while (
        current < this.fixedData.data.size() &&
        this.fixedData.data[current].key == key
      ) {
        (_path, values) = this.fixedData.data[current].value;
        path = Path(this.dirName, key);
        for (value in values) {
          subKey = getKey(value);
          !subKeys = subKeys.set(subKey);
          rows.push(FixedRow(subKey, (path, Array[value])));
        };
        !current = current + 1;
      };
      !keys[key] = subKeys;
      rows.sort();
      rows.toArray()
    };
    addDir = (key: BaseName, rows: Array<FixedRow<Array<File>>>) -> {
      time = context.timeStamp();
      dirName = Partition::dirName(baseDirName, key);
      source = Path(this.dirName, key);
      fixedData = FixedData{dirName, data => rows};
      dir = Dir{time, dirName, isInput => false, fixedData};
      context.setDir(dirName, dir);
      dirs.push(FixedRow(key, (source, Array<File>[dir])));
    };
    for (key => values in this.data) {
      while (
        current < this.fixedData.data.size() &&
        this.fixedData.data[current].key < key
      ) {
        fixedRow = this.fixedData.data[current];
        fixedFiles = getFixedFiles(fixedRow);
        addDir(fixedRow.key, fixedFiles);
      };
      while (
        current < this.fixedData.data.size() &&
        this.fixedData.data[current].key == key
      ) {
        !current = current + 1;
      };
      {
        rows = mutable Vector[];
        for (path => value in values) {
          rows.push(FixedRow(key, (path, value)));
        };
        addDir(key, rows.toArray())
      }
    };
    while (current < this.fixedData.data.size()) {
      fixedRow = this.fixedData.data[current];
      fixedFiles = getFixedFiles(fixedRow);
      addDir(fixedRow.key, fixedFiles);
    };
    dirName = DirName::create(baseDirName);
    time = context.timeStamp();
    fixedData = FixedData<Array<File>>{dirName, data => dirs.toArray()};
    subDirs = Dir{time, dirName, isInput => false, fixedData};
    partition = Partition{time, baseDirName, dirs => subDirs, getKey, keys};
    !this.partitions = this.partitions.add(time, partition);
    context.setDir(this.dirName, this);
    subDirs
  }

  private fun updateChildForKey<T: File>(
    mapFun: MapFun,
    context: mutable Context,
    oldKeys: mutable Set<BaseName>,
    source: Path,
    key: BaseName,
    values: Array<T>,
  ): (this, Array<BaseName>) {
    writer = mutable Writer{};
    mapFun(context, writer, key, values);
    mapped = freeze(writer).getWrites();
    reads = context.getReads();

    for (read in reads) {
      readSet = context.newDeps.maybeGet(read) match {
      | None() -> SortedSet[]
      | Some(x) -> x
      };
      !readSet = readSet.set((source.dirName, this.dirName, key));
      context.!newDeps[read] = readSet;
    };

    keys = mutable Vector[];

    for (kv in mapped) {
      (k, rvalues) = kv;

      keys.push(k);
      if (oldKeys.contains(k)) {
        oldKeys.remove(k);
      };
      !this = this.writeEntry(context, source, k, rvalues);
    };

    // Let's remove the keys that no longer exist.
    for (k in oldKeys) {
      !this = this.writeEntry(context, source, k, Array[]);
    };

    (this, keys.toArray())
  }

  fun updateDirtyReaders(context: mutable Context, path: Path): void {
    readers = context.getDeps(path);

    for (reader in readers) {
      (dirName, childName, baseName) = reader;
      dir = context.getDir(dirName);
      !dir.dirtyReaders = dir.dirtyReaders.set(Path(childName, baseName));
      if (
        dir.lazyFun is Some _ &&
        childName == dir.dirName &&
        childName != this.dirName
      ) {
        !dir.data = dir.data.remove(baseName);
      };
      context.setDir(dirName, dir);
      context.!toUpdate = context.toUpdate.set(dir.time, dirName);
    };
  }

  fun writeEntry(
    context: mutable Context,
    source: Path,
    k: BaseName,
    rvalues: Array<File>,
  ): this {
    map = this.data.maybeGet(k) match {
    | None() -> SortedMap[]
    | Some(x) -> x
    };
    irvalues = rvalues.map(x -> x);
    !map = map.set(source, irvalues);
    data = this.data.set(k, map);
    dirty = this.dirty.set(k);
    path = Path(this.dirName, k);
    this.updateDirtyReaders(context, path);

    !this = this with {data, dirty};
    newThis = this;
    context.setDir(this.dirName, this);
    context.!toUpdate = context.toUpdate.set(newThis.time, this.dirName);
    if (context.debugMode) {
      name = "" + this.dirName + k;
      print_string(`WRITTEN:  ${name}`);
    };
    this
  }

  fun writeArray(
    context: mutable Context,
    baseName: String,
    values: Array<File>,
  ): void {
    key = BaseName::create(baseName);
    source = Path(this.dirName, key);
    !this = this.writeEntry(context, source, key, values);
    context.setDir(this.dirName, this);
  }

  fun write(context: mutable Context, baseName: String, value: File): void {
    this.writeArray(context, baseName, Array[value]);
  }

  fun remove(context: mutable Context, baseName: String): void {
    this.writeArray(context, baseName, Array[]);
  }
}

module end;
