/*****************************************************************************/
/* The interface for directories. */
/*****************************************************************************/

module SKFS;

base class Dir extends File {
  /**
   * Returns true is the directory was deleted.
   */
  fun isDeleted(): Bool;

  /**
   * Returns true is the directory is an Input.
   */
  fun isInput(): Bool;

  /**
   * Returns the list of children.
   * A child directory is a directory that was the result of a map applied
   * to a parent directory.
   */
  fun getChildren(): Array<DirName>;

  /**
   * Returns an array of files associated with a key.
   * The array is empty if the key does not exist.
   */
  fun getArray(context: mutable Context, key: BaseName): Array<File>;

  /**
   * Same as getArray but does not create a dependency, which will break in
   * incremental mode. This should only be used for testing.
   */
  fun getArrayRaw(key: BaseName): Array<File>;

  /**
   * Returns the list of files in a directory. Should only be used for testing.
   */
  fun files(): Set<BaseName>;

  /**
   * Returns the directory name
   */
  fun getDirName(): DirName;

  /**
   * Returns the creation time of that directory.
   */
  fun getTime(): Int;
}

/*****************************************************************************/
/* Exception thrown by map */
/*****************************************************************************/

value class FixedRow<T>(
  key: BaseName,
  value: (Path, T),
) uses Hashable, Orderable {
  fun toKeyPath(): (BaseName, Path) {
    (this.key, this.value.i0);
  }

  fun compare(row: FixedRow<T>): Order {
    this.toKeyPath().compare(row.toKeyPath())
  }

  fun hash(): Int {
    this.toKeyPath().hash()
  }

  fun changeSource(source: Path): this {
    !this.value.i0 = source;
    this
  }
}

value class RowMapped<T>(
  key: BaseName,
  reads: Vector<Path>,
  values: Array<FixedRow<T>>,
  newDirs: SortedSet<DirName>,
)

value class GroupedRows<T>(
  rows: Array<FixedRow<T>>,
  old: Array<FixedRow<BaseName>>,
  newDirs: SortedMap<BaseName, SortedSet<DirName>>,
)

class ProductRow(
  key1: BaseName,
  key2: BaseName,
  values: Array<File>,
) extends File

class FixedData<T>{data: Array<FixedRow<T>> = Array[]} {
  fun getPos(key: BaseName, i: Int, j: Int): Int {
    if (i > j) {
      return i;
    };
    pivot = (i + j) / 2;
    key.compare(this.data[pivot].key) match {
    | LT()
    | EQ() ->
      this.getPos(key, i, pivot - 1)
    | GT() -> this.getPos(key, pivot + 1, j)
    }
  }

  fun getArray(key: BaseName): Array<(Path, T)> {
    pos = this.getPos(key, 0, this.data.size() - 1);
    result = mutable Vector[];
    while (pos < this.data.size() && this.data[pos].key == key) {
      result.push(this.data[pos].value);
      !pos = pos + 1;
    };
    result.toArray();
  }
}

module end;
