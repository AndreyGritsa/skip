/*****************************************************************************/
/* Lazy directories. */
/*****************************************************************************/
module SKFS;

base class LazyResult {
  children =
  | LDefined(Array<File>, Array<Path>)
  | LAbsent()
  | LDelayed()
}

class LazyDir{
  time: Int,
  dirName: DirName,
  data: SortedMap<BaseName, LazyResult> = SortedMap[],
  lazyFun: ((mutable Context, BaseName) ~> LazyResult),
} extends Dir {
  fun isInput(): Bool {
    false
  }

  fun getTime(): Int {
    this.time
  }

  fun reset(context: mutable Context): void {
    context.setDir(this.dirName, this);
  }

  fun keys(): Set<BaseName> {
    keys = mutable Set[];
    for (newKey => value in this.data) {
      if (value is LDefined _) {
        keys.insert(newKey);
      };
    };
    keys.chill()
  }

  fun getDirName(): DirName {
    this.dirName
  }

  fun isDeleted(): Bool {
    false
  }

  static fun create(
    context: mutable Context,
    dirName: DirName,
    f: (mutable Context, DirName, BaseName) ~> ?Array<File>,
    collect: Bool = true,
  ): Dir {
    time = context.timeStamp();
    lazyFun = (context: mutable Context, key: BaseName) ~> {
      readsCopy = context.reads;
      context.!reads = SortedSet[];

      resultOpt = f(context, dirName, key);

      result = resultOpt match {
      | None() -> LAbsent()
      | Some(x) -> LDefined(x, context.reads.toArray())
      };

      for (read in context.reads) {
        arrowKey = ArrowKey(dirName, dirName, key);
        context.!deps = context.deps.set(read, arrowKey);
      };

      dir = context.unsafeGetLazyDir(dirName);
      !dir.data[key] = result;
      if (collect) {
        context.!lazyGets = context.lazyGets.set(Path::create(dirName, key));
      };

      context.setDir(dirName, dir);
      if (context.debugMode) {
        name = "" + dir.dirName + key;
        print_string(`WRITTEN LAZY:  ${name} => ${inspect(result)}`);
      };
      context.!reads = readsCopy;
      result
    };
    newDir = LazyDir{time, dirName, lazyFun};
    updateDirtyReaders(context, Path::create(dirName.tag(), DIR_TAG));
    context.setDir(dirName, newDir);
    newDir
  }

  fun getArrayRaw(key: BaseName): Array<File> {
    this.data.maybeGet(key) match {
    | Some(LDefined(arr, _)) -> arr
    | _ -> Array[]
    }
  }

  fun update(
    context: mutable Context,
    dirtyReadersOpt: ?readonly Map<DirName, readonly Set<BaseName>>,
  ): void {
    thisDirtyReaders = SortedSet[];
    firstDirtyReaders = dirtyReadersOpt match {
    | None() -> mutable Map[]
    | Some(m) -> m
    };

    for (dirtyReaderDirName => dirtyReaderBaseNames in firstDirtyReaders) {
      for (dirtyReaderBaseName in dirtyReaderBaseNames) {
        !thisDirtyReaders = thisDirtyReaders.add(
          Path::create(dirtyReaderDirName, dirtyReaderBaseName),
        );
      }
    };
    dirtyReaders = fixPoint(thisDirtyReaders, key -> {
      context
        .getDeps(key)
        .filter(x -> x.childName == this.dirName)
        .map(x -> Path::create(x.childName, x.key))
        .collect(Set);
    });
    for (path in dirtyReaders) {
      key = path.baseName;
      newData = this.lazyFun(context, key);
      this.data.maybeGet(key) match {
      | Some(oldData) ->
        if (native_eq(oldData, newData) == 0) {
          if (context.debugMode) {
            print_string("LAZY REUSING KEY: " + key);
          };
          continue;
        }
      | None() -> void
      };
      !this.data[key] = newData;
      updateDirtyReaders(context, path);
    };
    context.setDir(this.dirName, this);
  }

  fun unsafeGetArray(
    context: mutable Context,
    key: BaseName,
    isAsync: Bool = false,
  ): Array<File> {
    lazyResult = this.data.maybeGet(key) match {
    | None()
    | Some(LAbsent()) ->
      if (isAsync) {
        context.pushAsync(Path::create(this.dirName, key));
        LDelayed()
      } else {
        this.lazyFun(context, key)
      }
    | Some(LDelayed()) if (!isAsync) -> this.lazyFun(context, key)
    | Some(x) -> x
    };
    lazyResult match {
    | LDefined(arr, reads) ->
      for (read in reads) {
        context.!reads = context.reads.set(read);
      };
      arr
    | _ -> Array[]
    }
  }

  fun getArray(context: mutable Context, key: BaseName): Array<File> {
    path = Path::create(this.dirName, key);
    context.addDep(path);
    this.unsafeGetArray(context, key, false)
  }

  fun getIter(context: mutable Context, key: BaseName): mutable Iterator<File> {
    this.getArray(context, key).iterator()
  }

  fun getArrayAsync(context: mutable Context, key: BaseName): Array<File> {
    path = Path::create(this.dirName, key);
    context.addDep(path);
    this.unsafeGetArray(context, key, true)
  }
}
