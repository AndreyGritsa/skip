/*****************************************************************************/
/* File defining the context.
 *
 * The context is almost always mutable (unless it is copied to the shared
 * heap).
 *
 * It keeps track of all the manipulation of the file system. Directories,
 * dependencies etc ...
 */
/*****************************************************************************/

module SKFS;

/*****************************************************************************/
/* Constants */
/*****************************************************************************/

const debugMode: Bool = false;

// Unused constants. Are defined because of a bug in the compiler ... Sigh.

const dummyDirty: Map<DirName, Set<BaseName>> = Map[];
const dummyDirtyReaders: Map<DirName, Map<DirName, Set<BaseName>>> = Map[];

/*****************************************************************************/
/* The types used to manage dependencies. */
/*****************************************************************************/

// An Arrow represent an operation from a parent to a child.
// Typically, a map is a tranformation that associates a value from a parent
// to a child. The association between the two is called an Arrow.

value class Arrow(
  parentName: DirName,
  childName: DirName,
) uses Hashable, Orderable

// An ArrowKey, is an Arrow plus a key (the key is from the parent).
// This type is used to describe the root of a dependency on an EagerMap.
// Let's say we have a directory A mapped to B. So B is the result of a map
// applied to A with a given function.

// If during the transformation from A to B, a key is read in third directory,
// we will need to remember that the read occured while processing a map
// from A to B on the key K. Hence the type: ArrowKey(A, B, K).

value class ArrowKey(
  parentName: DirName,
  childName: DirName,
  key: BaseName,
) uses Hashable, Orderable

/*****************************************************************************/
/* The function used to start the system. */
/*****************************************************************************/

fun run(f: (mutable Context) -> void): mutable Context {
  context = mutable Context{};
  f(context);
  context.updateNewDeps();
  context.initPre();
  context.updateFixedDeps();
  context
}

/*****************************************************************************/
/* Primitives used to make a file "dirty" (dirty means that it needs to be
 * recomputed.
 *
 * - dirtyReaders are files that are made dirty because of a read access
 * - dirty are files that are made dirty because of a Map.
 */
/*****************************************************************************/

fun updateDirtyReaders(context: mutable Context, path: Path): void {
  for (reader in context.getDeps(path)) {
    child = context.unsafeGetDir(reader.childName);
    addDirtyReader(context, reader);
    context.!toUpdate = context.toUpdate.set(
      child.getTime(),
      Arrow(reader.parentName, reader.childName),
    );
  };
}

fun addDirtyReader(context: mutable Context, reader: ArrowKey): void {
  parentName = reader.parentName;
  childName = reader.childName;

  if (!context.dirtyReaders.containsKey(parentName)) {
    context.dirtyReaders![parentName] = mutable Map[];
  };

  if (!context.dirtyReaders[parentName].containsKey(childName)) {
    context.dirtyReaders[parentName]![childName] = mutable Set[];
  };
  context.dirtyReaders[parentName][childName].insert(reader.key);
}

/*****************************************************************************/
/* The context */
/*****************************************************************************/

mutable class Context{
  mutable pwd: String = "/",
  mutable dirs: Dirs = Dirs{},
  mutable lazyVisitedList: List<Path> = List[],
  mutable lazyVisitedSet: SortedSet<Path> = SortedSet[],
  mutable reads: mutable Vector<Path> = mutable Vector[],
  mutable time: Int = 1,
  mutable tick: Int = 1,
  mutable toUpdate: SortedMap<Int, Arrow> = SortedMap[],
  mutable deps: List<Array<(Path, ArrowKey)>> = List[],
  mutable fixedDeps: Map<Path, Set<ArrowKey>> = Map[],
  mutable newDeps: SortedMap<Path, SortedSet<ArrowKey>> = SortedMap[],
  mutable debugMode: Bool = debugMode,
  mutable failOnExn: Bool = true,
  mutable hasPre: SortedSet<DirName> = SortedSet[],
  mutable hasNext: SortedSet<DirName> = SortedSet[],
  mutable newDirs: SortedSet<DirName> = SortedSet[],
  mutable toReset: SortedSet<DirName> = SortedSet[],
  mutable globals: SortedMap<String, File> = SortedMap[],
  mutable asyncList: List<Path> = List[],
  dirty: mutable Map<DirName, mutable Set<BaseName>> = mutable Map[],
  dirtyReaders: mutable Map<
    DirName,
    mutable Map<DirName, mutable Set<BaseName>>,
  > = mutable Map[],
} {
  mutable fun pushAsync(path: Path): void {
    this.!asyncList = List.Cons(path, this.asyncList);
  }

  mutable fun popAsync(): ?Path {
    this.asyncList match {
    | List.Nil() -> None()
    | List.Cons(x, rl) ->
      this.!asyncList = rl;
      Some(x)
    }
  }

  readonly fun clone(): this {
    static{
      pwd => this.pwd,
      dirs => this.dirs,
      lazyVisitedList => this.lazyVisitedList,
      lazyVisitedSet => this.lazyVisitedSet,
      reads => Vector[],
      time => this.time,
      tick => this.tick,
      toUpdate => this.toUpdate,
      deps => this.deps,
      fixedDeps => this.fixedDeps,
      newDeps => this.newDeps,
      debugMode => this.debugMode,
      hasPre => this.hasPre,
      hasNext => this.hasNext,
      newDirs => this.newDirs,
      toReset => this.toReset,
      globals => this.globals,
    }
  }

  static fun fromSaved(ctx: Context): mutable this {
    mutable static{
      pwd => ctx.pwd,
      dirs => ctx.dirs,
      lazyVisitedList => ctx.lazyVisitedList,
      lazyVisitedSet => ctx.lazyVisitedSet,
      reads => mutable Vector[],
      time => ctx.time,
      tick => ctx.tick,
      toUpdate => ctx.toUpdate,
      deps => ctx.deps,
      fixedDeps => ctx.fixedDeps,
      newDeps => ctx.newDeps,
      debugMode => ctx.debugMode,
      hasPre => ctx.hasPre,
      hasNext => ctx.hasNext,
      newDirs => ctx.newDirs,
      toReset => ctx.toReset,
      globals => ctx.globals,
    }
  }

  mutable fun getReads(): Vector<Path> {
    reads = this.reads.chill();
    reads
  }

  readonly fun getDeps(path: Path): Set<ArrowKey> {
    set = if (!this.fixedDeps.containsKey(path)) {
      Set[]
    } else {
      this.fixedDeps[path]
    };
    this.newDeps.maybeGet(path) match {
    | None() -> set
    | Some(newDeps) ->
      mset = Set::mcreateFromItems(set);
      for (dep in newDeps) {
        mset.insert(dep);
      };
      mset.chill()
    }
  }

  readonly fun debug(path: Path): void {
    this.dirs.maybeGet(path.dirName) match {
    | None() -> print_string("Directory does not exist")
    | Some(dir) ->
      values = dir.getArrayRaw(path.baseName);
      debug(values)
    }
  }

  readonly fun getValue(path: Path): Array<File> {
    dir = this.dirs[path.dirName];
    values = dir.getArrayRaw(path.baseName);
    values
  }

  mutable fun updateFixedDeps(): void {
    fixedDeps = mutable Map[];
    invariant(this.fixedDeps.isEmpty());
    for (depVector in this.deps) {
      for (read in depVector) {
        (path, arrowKey) = read;
        sourceDir = arrowKey.parentName;
        targetDir = arrowKey.childName;
        baseName = arrowKey.key;
        if (!fixedDeps.containsKey(path)) {
          fixedDeps![path] = mutable Set[];
        };
        fixedDeps[path].add(ArrowKey(sourceDir, targetDir, baseName));
      }
    };
    this.!deps = List[];
    this.!fixedDeps = fixedDeps.chill().map((_, v) -> v.chill());
  }

  mutable fun updateNewDeps(): void {
    for (depVector in this.deps) {
      for (read in depVector) {
        (path, arrowKey) = read;
        sourceDir = arrowKey.parentName;
        targetDir = arrowKey.childName;
        baseName = arrowKey.key;
        reader = ArrowKey(sourceDir, targetDir, baseName);
        if (this.fixedDeps.containsKey(path)) {
          if (this.fixedDeps[path].contains(reader)) {
            continue;
          };
        };
        if (!this.newDeps.containsKey(path)) {
          this.!newDeps[path] = SortedSet[];
        };
        this.!newDeps[path] = this.newDeps[path].add(reader);
      }
    };
    this.!deps = List[];
  }

  mutable fun initPre(): void {
    this.updatePre(this.dirs.state.items().map(x -> x.i0).collect(SortedSet));
  }

  mutable fun updatePre(toUpdate: SortedSet<DirName>): void {
    if (this.debugMode) {
      print_string(`------------ UPDATE PRE ------------`);
    };

    for (dirName in toUpdate) {
      if (!this.hasPre.contains(dirName)) continue;
      preDirName = dirName.pre();
      dir = this.unsafeGetEagerDir(dirName);
      this.unsafeMaybeGetEagerDir(preDirName) match {
      | None() ->
        this.!newDirs = this.newDirs.add(preDirName);
        time = this.timeStamp();
        preDir = EagerDir{
          time,
          input => false,
          dirName => preDirName,
          totalSize => 0,
          fixedData => FixedDir<Array<File>, FixedRow<Array<File>>>{
            data => Array[],
            decompress => (_, x) ~> x,
          },
        };
        dir.unsafeIterKeys((key, _time) -> {
          values = dir.getArrayRaw(key);
          !preDir = preDir.writeEntry(
            this,
            Path::create(preDir.dirName, key),
            key,
            values,
          );
        });
        this.setDir(preDir.dirName, preDir);
        updateDirtyReaders(this, Path::create(preDirName.tag(), DIR_TAG));
        this.setDir(preDirName, preDir)
      | Some(preDir) ->
        for (kv in dir.data.getChangesAfter(this.tick - 1)) {
          (key, _) = kv;
          values = dir.getArrayRaw(key);
          !preDir = preDir.writeEntry(
            this,
            Path::create(preDir.dirName, key),
            key,
            values,
          );
        };
        this.setDir(preDir.dirName, preDir)
      }
    }
  }

  mutable fun update(): void {
    hasBackgroundWork = this.updateWithStatus(0);
    if (hasBackgroundWork) {
      invariant_violation(
        "update should not be called when there is background work" +
          "use updateWithStatus instead",
      );
    };
    void
  }

  mutable fun updateWithStatus(backgroundRounds: Int): Bool {
    if (this.debugMode) {
      print_string(`------------ UPDATE (TICK: ${this.tick}) ------------`);
    };

    this.updateNewDeps();

    this.!tick = this.tick + 1;

    for (_ in Range(0, backgroundRounds)) {
      path = this.popAsync() match {
      | None() -> break void
      | Some(x) -> x
      };
      dir = this.unsafeGetLazyDir(path.dirName);
      _ = dir.unsafeGetArray(this, path.baseName);
      updateDirtyReaders(this, path);
    };

    loop {
      toUpdate = this.toUpdate.minimum();
      toUpdate match {
      | None() ->
        this.dirty.clear();
        this.dirtyReaders.clear();
        this.updatePre(this.toReset);
        this.!toReset = SortedSet[];
        return !this.asyncList.isEmpty()
      | Some((time, Arrow(parentName, childName))) ->
        this.!toReset = this.toReset.set(childName);
        this.!toUpdate = this.toUpdate.remove(time);
        this.unsafeGetDir(childName) match {
        | DeletedDir _ -> void
        | child @ LazyDir _ -> child.update(this)
        | child @ EagerDir _ ->
          this.!toReset = this.toReset.set(parentName);
          EagerDir::update(this, parentName, child)
        }
      }
    }
  }

  mutable fun timeStamp(): Int {
    this.!time = this.time + 1;
    this.time
  }

  mutable fun mkdir<T: File>(
    conv: File ~> T,
    dirName: DirName,
    content: Array<(BaseName, T)> = Array[],
  ): EHandle<T> {
    this.mkdirMulti(
      dirName,
      content.map(kv -> {
        (key, value) = kv;
        (key, Array[value])
      }),
    );
    EHandle(conv, dirName)
  }

  mutable fun mkdirMulti(
    dirName: DirName,
    content: Array<(BaseName, Array<File>)> = Array[],
  ): void {
    if (this.dirs.state.containsKey(dirName)) {
      throw DirAlreadyExists(dirName);
    };
    vector: mutable Vector<FixedRow<Array<File>>> = mutable Vector[];
    for (kv in content) {
      (key, values) = kv;
      path = Path::create(dirName, key);
      vector.push(FixedRow(key, (path, values)));
    };
    vector.sort();
    fixedData = FixedDir{data => vector.toArray(), decompress => (_, x) ~> x};

    time = this.timeStamp();
    totalSize = fixedData.data.size();
    dir = EagerDir{time, input => true, dirName, fixedData, totalSize};
    this.!dirs = this.dirs.set(dir.time, dirName, dir);
    this.!newDirs = this.newDirs.add(dirName);
  }

  mutable fun mkdirCompressed<T: File, Row: frozen>(
    conv: File ~> T,
    dirName: DirName,
    content: Array<Row>,
    decompress: (Int, Row) ~> FixedRow<Array<File>>,
  ): EHandle<T> {
    if (this.dirs.state.containsKey(dirName)) {
      throw DirAlreadyExists(dirName);
    };
    fixedData = FixedDir{data => content, decompress};

    time = this.timeStamp();
    totalSize = fixedData.data.size();
    dir = EagerDir{time, input => true, dirName, fixedData, totalSize};
    this.!dirs = this.dirs.set(dir.time, dirName, dir);
    this.!newDirs = this.newDirs.add(dirName);
    EHandle(conv, dirName)
  }

  mutable fun setDir(dirName: DirName, dir: Dir): void {
    /*
    dirNamePwdStr = this.pwd.join("/");
    dirNameStr = dirName.toString();
    dirNameArr = dirNameStr.split("/");
    if (dirNameArr.size() < 2) {
      error(`Invalid directory name: ${dirNameStr}`);
    };
    lastDirName = dirNameArr[dirNameArr.size() - 2];
    if (dirNamePwdStr + lastDirName + "/" != dirNameStr) {
      error(`Cannot create directory ${dirNameStr} from path ${dirNamePwdStr}`);
    };
    */
    // TODO: re-enable this when the dirNames are represented as lists
    this.!dirs = this.dirs.set(dir.getTime(), dirName, dir);
  }

  mutable fun getDir(dirName: DirName): Dir {
    this.addDep(Path::create(dirName.tag(), DIR_TAG));
    this.dirs.get(dirName);
  }

  readonly fun unsafeGetDir(dirName: DirName): Dir {
    this.dirs.get(dirName);
  }

  mutable fun unsafeGetLazyDir(dirName: DirName): LazyDir {
    this.unsafeGetDir(dirName) match {
    | d @ LazyDir _ -> d
    | _ -> invariant_violation("unsafeGetLazyDir: Was expecting LazyDir")
    }
  }

  mutable fun unsafeGetEagerDir(dirName: DirName): EagerDir {
    this.unsafeGetDir(dirName) match {
    | d @ EagerDir _ -> d
    | _ -> invariant_violation("unsafeGetEagerDir: Was expecting EagerDir")
    }
  }

  mutable fun unsafeMaybeGetLazyDir(dirName: DirName): ?LazyDir {
    this.unsafeMaybeGetDir(dirName) match {
    | None()
    | Some(DeletedDir _) ->
      None()
    | Some(d @ LazyDir _) -> Some(d)
    | _ -> invariant_violation("unsafeMaybeGetLazyDir: Was expecting LazyDir")
    }
  }

  mutable fun unsafeMaybeGetEagerDir(dirName: DirName): ?EagerDir {
    this.unsafeMaybeGetDir(dirName) match {
    | None()
    | Some(DeletedDir _) ->
      None()
    | Some(d @ EagerDir _) -> Some(d)
    | _ -> invariant_violation("unsafeMaybeGetEagerDir: Was expecting EagerDir")
    }
  }

  mutable fun getLazyDir(dirName: DirName): LazyDir {
    this.getDir(dirName) match {
    | d @ LazyDir _ -> d
    | _ -> invariant_violation("getLazyDir: Was expecting LazyDir")
    }
  }

  mutable fun getEagerDir(dirName: DirName): EagerDir {
    this.getDir(dirName) match {
    | d @ EagerDir _ -> d
    | _ -> invariant_violation("getEagerDir: Was expecting EagerDir")
    }
  }

  mutable fun maybeGetLazyDir(dirName: DirName): ?LazyDir {
    this.maybeGetDir(dirName) match {
    | None()
    | Some(DeletedDir _) ->
      None()
    | Some(d @ LazyDir _) -> Some(d)
    | _ -> invariant_violation("maybeGetLazyDir: Was expecting LazyDir")
    }
  }

  mutable fun maybeGetEagerDir(dirName: DirName): ?EagerDir {
    this.maybeGetDir(dirName) match {
    | None()
    | Some(DeletedDir _) ->
      None()
    | Some(d @ EagerDir _) -> Some(d)
    | _ -> invariant_violation("maybeGetEagerDir: Was expecting EagerDir")
    }
  }

  mutable fun removeDir(dirName: DirName): void {
    if (this.debugMode) {
      print_string("REMOVED: " + dirName);
    };
    this.setDir(dirName, DeletedDir{dirName, time => this.timeStamp()});
  }

  mutable fun maybeGetDir<T: File>(dirName: DirName): ?Dir {
    this.addDep(Path::create(dirName.tag(), DIR_TAG));
    this.dirs.maybeGet(dirName);
  }

  readonly fun unsafeMaybeGetDir<T: File>(dirName: DirName): ?Dir {
    this.dirs.maybeGet(dirName);
  }

  mutable fun addDep(path: Path): void {
    this.reads.push(path)
  }

  mutable fun setGlobal(name: String, file: File): void {
    this.!globals = this.globals.set(name, file);
  }

  readonly fun getGlobal(name: String): ?File {
    this.globals.maybeGet(name)
  }
}

class Dirs{state: DMap<DirName, Dir> = DMap::empty()} {
  fun set<T: File>(time: Int, key: DirName, value: Dir): this {
    !this.state = this.state.set(TimeTag::create(time), key, value);
    this
  }
  fun maybeGet(key: DirName): ?Dir {
    this.state.maybeGet(key)
  }
  fun get(key: DirName): Dir {
    this.maybeGet(key) match {
    | None() -> invariant_violation("Directory not found: " + key)
    | Some(x) -> x
    }
  }
}

@cpp_extern
private native base class Obstack

@debug
@cpp_extern("SKIP_new_Obstack")
private native fun newObstack(): Obstack;

@debug
@cpp_extern("SKIP_destroy_Obstack")
private native fun destroyObstack(Obstack): void;

@debug
@cpp_extern("SKIP_destroy_Obstack_with_value")
private native fun destroyObstackWithValue<T: frozen>(
  Obstack,
  Array<T>,
): Array<T>;

@debug
@cpp_extern("SKIP_context_sync")
private native fun gContextSync(Context): Context;

@debug
@cpp_extern("SKIP_context_init")
private native fun gContextInit(Context): Context;

@debug
@cpp_extern("SKIP_context_get")
native fun gContextGet(): Context;

@debug
@cpp_extern("sk_verify_shared")
native fun .verify(Context): void;

@cpp_noinline
fun runWithGc(origContext: Context, f: Context ~> ?Context): void {
  context = gContextInit(origContext);
  loop {
    pos = newObstack();
    verify(context);
    newContextOpt = f(context);
    newContextOpt match {
    | None() -> return void
    | Some(newContext) ->
      !context = gContextSync(newContext);
      destroyObstack(pos)
    }
  }
}

fun withRegion<T: frozen>(f: () ~> T): T {
  saved = newObstack();
  result = Array[f()];
  copied = destroyObstackWithValue(saved, result);
  copied[0]
}

module end;
