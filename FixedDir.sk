/*****************************************************************************/
/* Definition of the "fixed" part of a directory.
 *
 * Every Eager Directory defines a "fixed" part. Meaning, the values defined
 * at initialization time. After the initialization, the directory is defined
 * as the difference between the initialization data (that never changes) and
 * the changes made to the directory since its inception.
 *
 * So for example:
 * dir = mkdir(..., Array[(1, 1), (2, 2), (3, 3)]);
 * fixedData => FixedRow((1, (src, 1)), (2, (src, 2)), (3, (src, 3)))
 *
 * Then later someone removes the key 2.
 * data => DMap[2 => SortedMap(src, Array[])]
 *
 * And the value associated with the key "2" is defined as the difference
 * between fixedData and data. So in this case, because the source is the same
 * the key 2 is associated with an empty array.
 */
/*****************************************************************************/
module SKFS;

value class FixedRow<T>(
  key: BaseName,
  value: (Path, T),
) uses Hashable, Orderable {
  fun toKeyPath(): (BaseName, Path) {
    (this.key, this.value.i0);
  }

  fun compare<U>[T: U](row: FixedRow<U>): Order {
    this.toKeyPath().compare(row.toKeyPath())
  }

  fun hash(): Int {
    this.toKeyPath().hash()
  }

  fun changeSource(source: Path): this {
    !this.value.i0 = source;
    this
  }
}

base class FixedData<T> {
  fun size(): Int;
  fun get(idx: Int): FixedRow<T>;
  fun getArray(key: BaseName): Array<(Path, T)>;
  fun getIter(key: BaseName): mutable Iterator<(Path, T)>;
  fun getArraySourceKey(source: Path, key: BaseName): Array<T>;
}

class CompressedDir<T, Compressed>{
  data: Array<Compressed> = Array[],
  decompress: (Int, Compressed) ~> FixedRow<T>,
} extends FixedData<T> {
  fun size(): Int {
    this.data.size()
  }

  fun get(idx: Int): FixedRow<T> {
    this.decompress(idx, this.data[idx])
  }

  fun getArray(key: BaseName): Array<(Path, T)> {
    pos = key match {
    | IID(x) -> x
    | _ -> invariant_violation("Expected a primary key")
    };
    result = mutable Vector[];
    loop {
      if (pos >= this.data.size()) break void;
      elt = this.decompress(pos, this.data[pos]);
      if (elt.key != key) break void;
      result.push(elt.value);
      !pos = pos + 1;
    };
    result.toArray();
  }

  fun getIter(key: BaseName): mutable Iterator<(Path, T)> {
    pos = key match {
    | IID(x) -> x
    | _ -> invariant_violation("Expected a primary key")
    };
    loop {
      if (pos >= this.data.size()) break void;
      elt = this.decompress(pos, this.data[pos]);
      if (elt.key != key) break void;
      yield elt.value;
      !pos = pos + 1;
    };
  }

  fun getIterSourceKey(source: Path, key: BaseName): mutable Iterator<T> {
    pos = key match {
    | IID(x) -> x
    | _ -> invariant_violation("Expected a primary key")
    };
    loop {
      if (pos >= this.data.size()) break void;
      entry = this.decompress(pos, this.data[pos]);
      if (entry.value.i0 != source) break void;
      if (entry.key != key) break void;
      yield entry.value.i1;
      !pos = pos + 1;
    };
  }

  fun getArraySourceKey(source: Path, key: BaseName): Array<T> {
    this.getIterSourceKey(source, key).collect(Array)
  }
}

class FixedDir<T>{data: Array<FixedRow<T>> = Array[]} extends FixedData<T> {
  fun size(): Int {
    this.data.size()
  }

  fun get(idx: Int): FixedRow<T> {
    this.data.unsafe_get(idx)
  }

  fun getPos(key: BaseName, i: Int, j: Int): Int {
    if (i > j) {
      return i;
    };
    pivot = (i + j) / 2;
    elt = this.data[pivot];
    key.compare(elt.key) match {
    | LT()
    | EQ() ->
      this.getPos(key, i, pivot - 1)
    | GT() -> this.getPos(key, pivot + 1, j)
    }
  }

  fun getPosSourceKey(source: Path, key: BaseName, i: Int, j: Int): Int {
    if (i > j) {
      return i;
    };
    pivot = (i + j) / 2;
    entry = this.data[pivot];
    c1 = key.compare(entry.key);
    c = if (c1 is EQ _) source.compare(entry.value.i0) else c1;
    c match {
    | LT()
    | EQ() ->
      this.getPosSourceKey(source, key, i, pivot - 1)
    | GT() -> this.getPosSourceKey(source, key, pivot + 1, j)
    }
  }

  fun getArray(key: BaseName): Array<(Path, T)> {
    pos = this.getPos(key, 0, this.data.size() - 1);
    result = mutable Vector[];
    loop {
      if (pos >= this.data.size()) break void;
      elt = this.data[pos];
      if (elt.key != key) break void;
      result.push(elt.value);
      !pos = pos + 1;
    };
    result.toArray();
  }

  fun getIter(key: BaseName): mutable Iterator<(Path, T)> {
    pos = this.getPos(key, 0, this.data.size() - 1);
    loop {
      if (pos >= this.data.size()) break void;
      elt = this.data[pos];
      if (elt.key != key) break void;
      yield elt.value;
      !pos = pos + 1;
    };
  }

  fun getIterSourceKey(source: Path, key: BaseName): mutable Iterator<T> {
    pos = this.getPosSourceKey(source, key, 0, this.data.size() - 1);
    loop {
      if (pos >= this.data.size()) break void;
      entry = this.data[pos];
      if (entry.value.i0 != source) break void;
      if (entry.key != key) break void;
      yield entry.value.i1;
      !pos = pos + 1;
    };
  }

  fun getArraySourceKey(source: Path, key: BaseName): Array<T> {
    this.getIterSourceKey(source, key).collect(Array)
  }
}

mutable class FixedDataIterator<T> private {
  iter: mutable Iterator<(Path, T)>,
  mutable currentOpt: ?(Path, T) = None(),
} {
  static fun create(iter: mutable Iterator<(Path, T)>): mutable this {
    mutable static{currentOpt => iter.next(), iter => iter}
  }

  readonly fun isEnd(): Bool {
    this.currentOpt is None _
  }

  readonly fun current(): (Path, T) {
    this.currentOpt match {
    | None() -> invariant_violation("Cannot access current")
    | Some(x) -> x
    }
  }

  mutable fun next(): void {
    this.!currentOpt = this.iter.next();
  }
}
