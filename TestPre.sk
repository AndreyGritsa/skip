module SKFSTest;

fun write(
  context: mutable SKFS.Context,
  dirInput: SKFS.Handle<SKFS.StringFile>,
  key: SKFS.BaseName,
  values: Array<SKFS.StringFile>,
): void {
  context.unsafeGetDir(dirInput.dirName).writeArray(context, key, values);
}

fun getData(
  context: mutable SKFS.Context,
  dirName: SKFS.DirName,
  key: SKFS.BaseName,
): Array<SKFS.File> {
  context.unsafeGetDir(dirName).getArrayRaw(key);
}

fun testPre(): mutable SKFS.Context {
  SKFS.run(context ~> {
    context.!debugMode = true;
    dirSInput1 = context.mkdir(
      SKFS.StringFile::fromFile,
      SKFS.DirName::create("/sinput/"),
      Array[
        (SKFS.IID(0), SKFS.StringFile("23")),
        (SKFS.IID(1), SKFS.StringFile("35")),
      ],
    );

    _dirSInput2 = context.mkdir(
      SKFS.StringFile::fromFile,
      SKFS.DirName::create("/sinput2/"),
      Array[
        (SKFS.IID(0), SKFS.StringFile("44")),
        (SKFS.IID(1), SKFS.StringFile("45")),
      ],
    );

    dirInput = dirSInput1.map(
      SKFS.IntFile::fromFile,
      context,
      SKFS.DirName::create("/input/"),
      x ~> SKFS.IntFile(x.value.toInt()),
    );
    _dir = dirInput.contextWriterKeyMap(
      SKFS.IntFile::fromFile,
      context,
      SKFS.DirName::create("/diff/"),
      (context, writer, key, valueFile) ~> {
        preOpt = dirInput.pre(context);
        preOpt match {
        | None() -> writer.write(key, SKFS.IntFile(0))
        | Some(pre) ->
          pre.maybeGet(context, key) match {
          | None() -> writer.write(key, SKFS.IntFile(0))
          | Some(old) ->
            diff = valueFile.value - old.value;
            writer.write(key, SKFS.IntFile(diff))
          }
        }
      },
    );

    write(context, dirSInput1, SKFS.IID(3), Array[SKFS.StringFile("3")]);
    context.update();
    write(context, dirSInput1, SKFS.IID(4), Array[SKFS.StringFile("4")]);
    context.update();
    expectEq("Test pre 1", Array<SKFS.File>[SKFS.IntFile(3)], () ->
      getData(context, dirInput.pre(context).fromSome().dirName, SKFS.IID(3))
    );
    write(context, dirSInput1, SKFS.IID(3), Array[]);
    context.update();
    expectEq("Test pre 2", Array<SKFS.File>[SKFS.IntFile(4)], () ->
      getData(context, dirInput.pre(context).fromSome().dirName, SKFS.IID(4))
    );
    write(context, dirSInput1, SKFS.IID(4), Array[]);
    context.update();
    expectEq("Test pre 3", Array<SKFS.File>[], () ->
      getData(context, dirInput.pre(context).fromSome().dirName, SKFS.IID(3))
    )
  });
}
