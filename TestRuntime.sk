module SKFSTest;

class MyEx(value: Int) extends Exception

fun makeExn(): Int {
  throw MyEx(22);
}

@cpp_extern("SKIP_SKFSTest_Obstack")
native fun testObstack<T: frozen>(T): void;

value class Foo(X, X, Int)

class X(Int, Int) extends SKFS.File
class T(Int, X) extends SKFS.File
class Y(X, Int) extends SKFS.File
class Z(Foo, Foo) extends SKFS.File

fun makeX(x: Int): X {
  X(x, x)
}

fun makeY(x: Int): Y {
  Y(X(x, x), x)
}

fun makeZ(n: Int): Z {
  x = X(n, n);
  Z(Foo(x, x, 22), Foo(x, x, 22))
}

fun makeT(x: Int): T {
  T(x + 1, makeX(x))
}

fun testRuntime(): void {
  chars = Array['a', 'b', 'c'];
  expectEq("String::replace", "xbc", () ->
    String::fromChars(chars).replace("a", "x")
  );
  str = "abc";
  str2 = "123";
  expectEq("String LT", (LT() : Order), () -> str.compare(str2));
  expectEq("String LT", (EQ() : Order), () -> str.compare("abc"));
  expectEq("String LT", (GT() : Order), () -> str2.compare(str));
  expectEq("String.concat2", "abc123", () -> str + str2);
  expectEq("String.byteSize", 3, () -> String.byteSize(str));
  expectEq("throw", 22, () -> {
    vtry(
      () -> makeExn(),
      exn -> {
        exn match {
        | MyEx(x) -> x
        | e -> throw e
        }
      },
    )
  });
  arr = Array[Array[1], Array[2], Array[3]];
  expectEq("getArraySize", 3, () -> getArraySize(arr));
  expectEq("hash", 96354, () -> str.hash());
  expectEq("String.getByte", 'b'.code(), () -> String.getByte(str, 1).toInt());
  expectEq("substring", "c", () -> {
    iter = str.getIter();
    str.substring(iter.drop(2));
  });
  expectEq("ArrayEach", 6, () -> {
    result = 0;
    Array[1, 2, 3].each(x -> !result = result + x);
    result
  });
}
